plugins {
    id 'eclipse'
    id 'maven-publish'
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.2-SNAPSHOT" apply false
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

apply plugin: 'java'

group = 'cx.rain.synccraft'
archivesBaseName = 'SyncCraft'
version = '1.19.4-2.1.0'

if (System.getenv("JITPACK") == "true") {
    version += "-${System.getenv("VERSION")}"
}

var release = false
if (release || project.hasProperty("SC_RELEASE")) {
    version += '-release'
} else {
    version += '-dev'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

allprojects {
    apply plugin: 'java'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'
    apply plugin: 'dev.architectury.loom'

    architectury {
        platformSetupLoomIde()
        forge()
    }

    loom {
        silentMojangMappingsLicense()

        forge {
        }

        runs {
        }
    }

    processResources {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    repositories {
        maven {
            name = 'Jitpack'
            url = 'https://jitpack.io'
        }

        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }

        flatDir {
            dir 'libs'
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:1.19.4"
        forge "net.minecraftforge:forge:1.19.4-45.0.66"

        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-1.19.3:2023.03.12@zip")
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    implementation project(":sync")
}

shadowJar {
    dependencies {
        include project(":sync")

        exclude("forge-client-extra.jar")
    }
}

jar.finalizedBy('shadowJar')

jar {
    manifest {
        attributes([
                "Specification-Title"     : "synccraft",
                "Specification-Vendor"    : "qyl27",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "qyl27",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}
